!/*****************************************************************************/
! *
! *  Elmer, A Finite Element Software for Multiphysical Problems
! *
! *  Copyright 1st April 1995 - , CSC - IT Center for Science Ltd., Finland
! * 
! *  This program is free software; you can redistribute it and/or
! *  modify it under the terms of the GNU General Public License
! *  as published by the Free Software Foundation; either version 2
! *  of the License, or (at your option) any later version.
! * 
! *  This program is distributed in the hope that it will be useful,
! *  but WITHOUT ANY WARRANTY; without even the implied warranty of
! *  MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
! *  GNU General Public License for more details.
! *
! *  You should have received a copy of the GNU General Public License
! *  along with this program (in file fem/GPL-2); if not, write to the 
! *  Free Software Foundation, Inc., 51 Franklin Street, Fifth Floor, 
! *  Boston, MA 02110-1301, USA.
! *
! *****************************************************************************/
!
!/******************************************************************************
! *
! *  Module for extracting isosurfaces
! *
! ******************************************************************************
! *
! *  Authors: Juha Ruokolainen
! *  Email:   jpr@csc.fi
! *  Web:     http://www.csc.fi/elmer
! *  Address: CSC - IT Center for Science Ltd.
! *           Keilaranta 14
! *           02101 Espoo, Finland 
! *
! *  Original Date: 20.06.2007
! *
! *****************************************************************************/

!------------------------------------------------------------------------------
  SUBROUTINE IsosurfaceSolver_Init( Model,Solver,dt,Transient )
!------------------------------------------------------------------------------
    USE DefUtils

    TYPE(Model_t)  :: Model
    TYPE(Solver_t) :: Solver
    REAL(KIND=dp) :: DT
    LOGICAL :: Transient
!------------------------------------------------------------------------------
    INTEGER :: dim
    TYPE(ValueList_t), POINTER :: SolverParams
!------------------------------------------------------------------------------
    SolverParams => GetSolverParams()
    IF ( .NOT. ListCheckPresent( SolverParams,'Variable') ) &
      CALL ListAddString( SolverParams,  'Variable', '-nooutput iso_tmp' )
!------------------------------------------------------------------------------
  END SUBROUTINE IsosurfaceSolver_Init
!------------------------------------------------------------------------------


!------------------------------------------------------------------------------
SUBROUTINE IsosurfaceSolver( Model,Solver,dt,Transient )
!------------------------------------------------------------------------------

  USE CoordinateSystems
  USE DefUtils

  IMPLICIT NONE
!------------------------------------------------------------------------------
!******************************************************************************
!
!  Search isosurfaces
!
!  ARGUMENTS:
!
!  TYPE(Model_t) :: Model,  
!     INPUT: All model information (mesh,materials,BCs,etc...)
!
!  TYPE(Solver_t) :: Solver
!     INPUT: Linear equation solver options
!
!  REAL(KIND=dp) :: dt,
!     INPUT: Timestep size for time dependent simulations (NOTE: Not used
!            currently)
!
!******************************************************************************

  TYPE(Model_t)  :: Model
  TYPE(Solver_t), TARGET :: Solver
  LOGICAL ::  Transient
  REAL(KIND=dp) :: dt
!------------------------------------------------------------------------------
!    Local variables
!------------------------------------------------------------------------------
  CHARACTER(LEN=MAX_NAME_LEN) :: LevelVariableName
  TYPE(Mesh_t), POINTER :: Mesh, IsoMesh => Null(), Pmesh
  INTEGER :: i,j,k,l,n
  REAL(KIND=dp) :: LevelValue

  INTEGER, TARGET :: TetraToTetraMap(1,4), PyramidToTetraMap(2,4), &
             WedgeToTetraMap(3,4), BrickToTetraMap(5,4)
  INTEGER, POINTER :: Map(:,:)
  INTEGER :: SaveNOFactive, SaveNOFBulk,calls=0
  INTEGER :: total, tetras, pyramids, wedges, bricks

  TYPE(Variable_t), POINTER :: LevelVariable
  LOGICAL :: Found
  INTEGER, ALLOCATABLE :: eperm(:)
  INTEGER, POINTER :: SaveActive(:)
  REAL(KIND=dp), ALLOCATABLE :: x(:),y(:),z(:)
  TYPE(Element_t), POINTER  :: SaveElements(:), NewElements(:), Element
 
  CALL Info( 'IsosurfaceSolver', '-------------------------------------',Level=4 )
  CALL Info( 'IsosurfaceSolver','Computing the isosurface',Level=4 )
  CALL Info( 'IsosurfaceSolver', '-------------------------------------',Level=4 )

  Mesh => GetMesh()

  LevelVariableName = GetString( GetSolverParams(), &
            'Isosurface Variable')

  LevelVariable => VariableGet( Mesh % Variables, & 
            LevelVariableName )

  IF (.NOT.ASSOCIATED(LevelVariable)) THEN
    CALL Error( 'Isosurface', 'The isosurface variable: ' // &
          TRIM(LevelVariableName) // ' not present' )
    RETURN
  END IF

  LevelValue = GetCReal( GetSolverParams(), &
     'Isosurface threshold', Found )

  TetraToTetraMap(1,:) = [1,2,3,4]

  PyramidToTetraMap(1,:) = [3,5,4,1]
  PyramidToTetraMap(2,:) = [3,5,2,1]

  WedgeToTetraMap(1,:) = [5,4,3,1]
  WedgeToTetraMap(2,:) = [5,3,2,1]
  WedgeToTetraMap(3,:) = [5,6,4,3]

  BrickToTetraMap(1,:) = [1,2,4,5]
  BrickToTetraMap(2,:) = [6,7,2,5]
  BrickToTetraMap(3,:) = [3,4,2,7]
  BrickToTetraMap(4,:) = [8,7,5,4]
  BrickToTetraMap(5,:) = [2,4,5,7]

  tetras=0; pyramids=0; wedges=0; bricks=0
  DO i=1,Solver % NumberOfActiveElements
    Element => GetActiveElement(i)
    SELECT CASE(GetElementFamily())
      CASE(5); tetras=tetras+1
      CASE(6); pyramids=pyramids+1
      CASE(7); wedges=wedges+1
      CASE(8); bricks=bricks+1
    END SELECT
  END DO
  total = tetras+2*pyramids+3*wedges+5*bricks

  IF ( total/=tetras ) THEN
    SaveNOFBulk = Mesh % NumberOfBulkElements
    SaveActive => Solver % ACtiveElements
    SaveElements => Mesh % Elements
    SaveNOFActive = Solver % NumberOFActiveElements

    ALLOCATE(NewElements(total))

    j = 0
    DO i=1,Solver % NumberOfActiveElements
      Element => GetActiveElement(i)

      SELECT CASE(GetElementFamily())
        CASE(5); Map => TetraToTetraMap
        CASE(6); Map => PyramidToTetraMap
        CASE(7); Map => WedgeToTetraMap
        CASE(8); Map => BrickToTetraMap
      END SELECT

      DO k=1,SIZE(Map,1)
        j = j + 1
        NewElements(j)=Element
        NewElements(j) % Type => GetElementType(504)
        ALLOCATE(NewElements(j) % NodeIndexes(4))
        DO l=1,4
           NewElements(j) % NodeIndexes(l) = &
              Element % NodeIndexes(Map(k,l))
        END DO
      END DO
    END DO
    Mesh % NumberOfBulkElements = total
    Mesh % Elements => NewElements
    ALLOCATE( Solver % ActiveElements(total) )
    DO i=1,total
      Solver % ActiveElements(i) = i
    END DO
    Solver % NumberOfActiveElements=total
  END IF

  IF (.NOT.ASSOCIATED(Mesh % Edges)) CALL FindMeshEdges3D(Mesh)
  n = Mesh % NumberOfEdges

  IF(ASSOCIATED(IsoMesh)) THEN
    Pmesh => Model % Meshes
    DO WHILE(ASSOCIATED(Pmesh % Next))
      IF ( ASSOCIATED(Pmesh % next, Isomesh)) THEN
        Pmesh % next=>Isomesh % Next
        EXIT
      END IF
    END DO
    CALL ReleaseMesh(Isomesh)
  END IF

  Isomesh => AllocateMesh()
  IsoMesh % Name = GetString( GetSolverParams(), & 
      'Isosurface output directory', Found )
  IF (.NOT. Found ) IsoMesh % Name = "isosurf"

  Calls = Calls + 1
  Isomesh % name = TRIM(Isomesh % name) // TRIM(i2s(calls))
  CALL MakeDirectory( TRIM(Isomesh % name) // CHAR(0) )

  Isomesh % Changed = .TRUE.
  Isomesh % OutputActive  = .TRUE.
  
  CALL CreateNodes()
  CALL CreateSurfaces()

  IF ( total/=tetras ) THEN
    DEALLOCATE(NewElements, Solver % ActiveElements)
    Mesh % NumberOfBulkElements = SaveNOFBulk
    Mesh % Elements => SaveElements
    Solver % ActiveElements => SaveActive
    Solver % NumberOfActiveElements = SaveNOFActive
  END IF

  Mesh => Model % Meshes
  DO WHILE( ASSOCIATED(Mesh % Next) )
    Mesh => Mesh % Next
  END DO
  Mesh % Next => Isomesh 

  CALL ReleaseMeshEdgeTables(Mesh)

CONTAINS


  SUBROUTINE CreateNodes()

    TYPE(Element_t), POINTER :: Edge
    INTEGER :: i,j,k,l,n1,n2,ints,comps
    REAL(KIND=dp) :: t,t1,t2,x1,x2,y1,y2,z1,z2

    CHARACTER(LEN=MAX_NAME_LEN) :: Vname(32)
    LOGICAL :: Found
    TYPE(Variable_t), POINTER :: V
    INTEGER, POINTER :: Vperm(:)
    REAL(KIND=dp), POINTER :: Vals(:), dummy(:)

    TYPE Varr_t
       REAL(KIND=dp), POINTER :: Vals(:)
    END TYPE Varr_t
    TYPE(Varr_t), POINTER :: Varr(:)


    ALLOCATE(x(n), y(n), z(n), eperm(n) )
    x = 0; y = 0; z = 0; eperm = 0;

    ints = 0
    DO WHILE(.TRUE.)
      ints = ints+1
      Vname(ints) = GetString( GetSolverParams(), &
          ComponentName('Isosurface interpolant',ints), Found )
      IF ( .NOT. Found ) THEN
        ints = ints-1
        EXIT
      END IF
    END DO

    IF ( ints>0 ) THEN
      ALLOCATE(Varr(ints))
      DO i=1,ints
        V => VariableGet( Mesh % Variables, Vname(i) )
        IF ( ASSOCIATED(V) ) THEN
          ALLOCATE(Varr(i) % vals(n*V%DOFs))
          Varr(i) % Vals = 0._dp
        END IF
      END DO
    END IF

    j = 0
    DO i=1,n
      Edge => Mesh % Edges(i)

      n1 = Edge % NodeIndexes(1)
      n2 = Edge % NodeIndexes(2)

      IF ( LevelVariable % Perm(n1)<=0 .OR. &
           LevelVariable % Perm(n2)<=0 ) CYCLE

      t1 = LevelVariable % Values(LevelVariable % Perm(n1))
      t2 = LevelVariable % Values(LevelVariable % Perm(n2))

      IF ( t1>LevelValue .AND. t2>LevelValue.OR. &
           t1<LevelValue .AND. t2<LevelValue) CYCLE 

      x1 = Mesh % Nodes % x(n1)
      x2 = Mesh % Nodes % x(n2)

      y1 = Mesh % Nodes % y(n1)
      y2 = Mesh % Nodes % y(n2)

      z1 = Mesh % Nodes % z(n1)
      z2 = Mesh % Nodes % z(n2)

      t = (LevelValue-t1)/(t2-t1)
      j = j+1
      eperm(i)=j
      x(j) = (1-t)*x1 + t*x2
      y(j) = (1-t)*y1 + t*y2
      z(j) = (1-t)*z1 + t*z2

      DO k=1,ints
        V => VariableGet(Mesh % Variables, Vname(ints))
        IF (ASSOCIATED(V)) THEN
          DO l=1,V % DOFs
            x1 = 0; x2 = 0;
            IF ( V % Perm(n1)>0 .AND. V % Perm(n2)>0 ) THEN
              x1  = V % Values(V % DOFs*(V % Perm(n1)-1)+l)
              x2  = V % Values(V % DOFs*(V % Perm(n2)-1)+l)
              Varr(k) % Vals(V % DOFs*(j-1)+l) = (1-t)*x1 + t*x2
            END IF
          END DO
        END IF
      END DO
    END DO

    
    ALLOCATE( IsoMesh % Nodes )
    Isomesh % NumberOfNodes = j
    Isomesh % Nodes % NumberOfNodes = j

    ALLOCATE( IsoMesh % Nodes % x(j) )
    ALLOCATE( IsoMesh % Nodes % y(j) )
    ALLOCATE( IsoMesh % Nodes % z(j) )

    IsoMesh % Nodes % x = x(1:j)
    IsoMesh % Nodes % y = y(1:j)
    IsoMesh % Nodes % z = z(1:j)
    DEALLOCATE(x,y,z)

    CALL VariableAdd( IsoMesh % Variables, IsoMesh,Solver, &
       'Coordinate 1',1,IsoMesh % Nodes % x )

    CALL VariableAdd( IsoMesh % Variables,IsoMesh,Solver, &
       'Coordinate 2',1,IsoMesh % Nodes % y )

    CALL VariableAdd( IsoMesh % Variables,IsoMesh,Solver, &
       'Coordinate 3',1,IsoMesh % Nodes % z )

    V => VariableGet( Mesh % Variables, 'Time' )
    CALL VariableAdd( Isomesh % Variables, Isomesh, Solver, 'Time', 1, &
        V % Values )

    IF ( ints>0 ) THEN
      DO i=1,ints
        V => VariableGet( Mesh % Variables, Vname(ints) )
        IF (ASSOCIATED(V)) THEN
          ALLOCATE( Vperm(j),Vals(V % DOFs*j) )
          Vperm = [(i,i=1,j)]
          Vals = Varr(i) % Vals(1:V % DOFs*j)
          DEALLOCATE( Varr(i) % Vals )
          CALL VariableAddVector( Isomesh % Variables, Isomesh, Solver, &
               TRIM(Vname(i)), V % DOFs, Vals, Vperm )
        END IF
      END DO
      DEALLOCATE(Varr)
    END IF
  END SUBROUTINE CreateNodes


  SUBROUTINE CreateSurfaces()
     TYPE(Element_t), POINTER :: Element, DefElement
     REAL(KIND=dp) :: F(4)
     INTEGER :: i,j,k,n,Triangles(2,3)
     INTEGER, ALLOCATABLE :: TriangleStore(:,:)

     ALLOCATE(TriangleStore(2*Solver % NumberOfActiveElements,3))
     k = 0
     DO i=1,Solver % NumberOfActiveElements
       Element => GetActiveElement(i)
       F(1:4) = LevelVariable % Values( &
           LevelVariable % Perm(Element % NodeIndexes) )
       n=CreateSurfaceFromTetra(Element,F,LevelValue,Triangles)

       DO j=1,n
         IF ( ANY(Triangles(j,:)<=0) ) CYCLE
         k = k + 1
         TriangleStore(k,:) = Triangles(j,:)
       END DO
     END DO
     ALLOCATE( Isomesh % Elements(k) )

     Isomesh % NumberOfBulkElements = k
     Isomesh % NumberOfFaces = 0
     Isomesh % NumberOfEdges = 0
     Isomesh % NumberOfBoundaryElements = 0

     DefElement => AllocateElement()
     DO i=1,k
       Isomesh % Elements(i) = DefElement
       Isomesh % Elements(i) % Type => GetElementType(303)
       Isomesh % Elements(i) % Bodyid = 1
       ALLOCATE(Isomesh % Elements(i) % NodeIndexes(3))
       Isomesh % Elements(i) % NodeIndexes = TriangleStore(i,:)
     END DO
     DEALLOCATE(DefElement, TriangleStore)
  END SUBROUTINE CreateSurfaces


  FUNCTION CreateSurfaceFromTetra(Tetra,F,K,Surf) RESULT(scount)
    TYPE(Element_t) :: Tetra
    REAL(KIND=dp) :: F(4), K
    INTEGER :: scount, Surf(2,3)

    LOGICAL :: S1,S2,S3,S4
    integer :: S,H,i,j,l,Indexes(6)

    scount = 0
    Indexes = eperm(Tetra % EdgeIndexes)

    S1 = F(1) > K;
    S2 = F(2) > K;
    S3 = F(3) > K;
    S4 = F(4) > K;

    S = 0
    IF ( S1 ) S=S+1
    IF ( S2 ) S=S+1
    IF ( S3 ) S=S+1
    IF ( S4 ) S=S+1

    IF ( S==0 .OR. S==4 ) RETURN

    IF ( S==1 .OR. S==3 ) THEN
      scount = 1
      IF ( (S==1 .AND. S1) .OR. (S==3 .AND. .NOT.S1) ) THEN
        Surf(1,1)=Indexes(1)
        Surf(1,2)=Indexes(3)
        Surf(1,3)=Indexes(4)
      ELSE IF ( (S==1 .AND. S2) .OR. (S==3 .AND. .NOT.S2) ) THEN
        Surf(1,1)=Indexes(1)
        Surf(1,2)=Indexes(2)
        Surf(1,3)=Indexes(5)
      ELSE IF ( (S==1 .AND. S3) .OR. (S==3 .AND. .NOT.S3) ) THEN
        Surf(1,1)=Indexes(2)
        Surf(1,2)=Indexes(3)
        Surf(1,3)=Indexes(6)
      ELSE IF ( (S==1 .AND. S4) .OR. (S==3 .AND. .NOT.S4) ) THEN
        Surf(1,1)=Indexes(4)
        Surf(1,2)=Indexes(5)
        Surf(1,3)=Indexes(6)
      ELSE
        scount=0
      END IF
    ELSE
      scount=2
      IF ( (S1 .AND. S2) .OR. (.NOT.S1 .AND. .NOT.S2) ) THEN
        Surf(1,1) = Indexes(3)
        Surf(1,2) = Indexes(2)
        Surf(1,3) = Indexes(5)

        Surf(2,1) = Indexes(3)
        Surf(2,2) = Indexes(5)
        Surf(2,3) = Indexes(4)
      ELSE IF ( (S1 .AND. S3) .OR. (.NOT.S1 .AND. .NOT.S3) ) THEN
        Surf(1,1) = Indexes(1)
        Surf(1,2) = Indexes(2)
        Surf(1,3) = Indexes(6)

        Surf(2,1) = Indexes(1)
        Surf(2,2) = Indexes(6)
        Surf(2,3) = Indexes(4)
      ELSE IF ( (S1 .AND. S4) .OR. (.NOT.S1 .AND. .NOT.S4) ) THEN
        Surf(1,1) = Indexes(1)
        Surf(1,2) = Indexes(5)
        Surf(1,3) = Indexes(6)

        Surf(2,1) = Indexes(1)
        Surf(2,2) = Indexes(6)
        Surf(2,3) = Indexes(3)
      ELSE
        scount=0
      END IF
    END IF
  END FUNCTION CreateSurfaceFromTetra

!------------------------------------------------------------------------------
END SUBROUTINE IsosurfaceSolver
!------------------------------------------------------------------------------
