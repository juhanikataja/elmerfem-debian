!/*****************************************************************************/
! *
! *  Elmer, A Finite Element Software for Multiphysical Problems
! *
! *  Copyright 1st April 1995 - , CSC - IT Center for Science Ltd., Finland
! * 
! *  This program is free software; you can redistribute it and/or
! *  modify it under the terms of the GNU General Public License
! *  as published by the Free Software Foundation; either version 2
! *  of the License, or (at your option) any later version.
! * 
! *  This program is distributed in the hope that it will be useful,
! *  but WITHOUT ANY WARRANTY; without even the implied warranty of
! *  MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
! *  GNU General Public License for more details.
! *
! *  You should have received a copy of the GNU General Public License
! *  along with this program (in file fem/GPL-2); if not, write to the 
! *  Free Software Foundation, Inc., 51 Franklin Street, Fifth Floor, 
! *  Boston, MA 02110-1301, USA.
! *
! *****************************************************************************/
!
!/******************************************************************************
! *
! *  Authors: Peter Råback
! *  Email:   Peter.Raback@csc.fi
! *  Web:     http://www.csc.fi/elmer
! *  Address: CSC - IT Center for Science Ltd.
! *           Keilaranta 14
! *           02101 Espoo, Finland 
! *
! *  Original Date: 3.3.2008
! *
! *****************************************************************************/

!------------------------------------------------------------------------------
!> Subroutine for projecting results in structured 3d mesh to a 2d surface.
!>  This solver assumes that the mesh is structural so that it could have 
!>  been obtained by extrusion in the direction of interest. For the given 
!>  direction the corresponding top and bottom node is computed for every node
!>  and this information is used to perform projection to the top or bottom
!>  plane.
!> \ingroup Solvers
!------------------------------------------------------------------------------
SUBROUTINE StructuredProjectToPlane( Model,Solver,dt,Transient )
!------------------------------------------------------------------------------
  USE CoordinateSystems
  USE DefUtils

  IMPLICIT NONE
!------------------------------------------------------------------------------
  TYPE(Model_t)  :: Model
  TYPE(Solver_t), TARGET :: Solver
  LOGICAL ::  Transient
  REAL(KIND=dp) :: dt
!------------------------------------------------------------------------------
!    Local variables
!------------------------------------------------------------------------------
  TYPE(ValueList_t),POINTER :: Params
  TYPE(Solver_t), POINTER :: PSolver
  TYPE(Mesh_t), POINTER :: Mesh
  CHARACTER(LEN=MAX_NAME_LEN) :: VarName, OldVarName, Name, Oper, LevelsetName, TargetName
  INTEGER :: i,j,k,l,n,dim,DOFs,itop,ibot,iup,jup,ii,jj,Rounds,nsize,layer, &
      ActiveDirection,elem,TopNodes,NoVar,BotNodes
  INTEGER, POINTER :: MaskPerm(:),TopPointer(:),BotPointer(:),UpPointer(:),DownPointer(:),&
      NodeIndexes(:),TargetPointer(:),BotPerm(:),&
      PermOut(:),PermIn(:),LevelsetPerm(:),TopPerm(:)
  LOGICAL :: GotIt, Found, Visited = .FALSE., Initialized = .FALSE.,&
      Debug, MaskExist, GotVar, GotOper, BottomTarget, TemporalLayer
  REAL(KIND=dp) :: dx,UnitVector(3),ElemVector(3),DotPro,Eps,Length,Level,val
  REAL(KIND=dp) :: at0,at1,at2,CPUTime,RealTime
  REAL(KIND=dp), POINTER :: FieldOut(:), FieldIn(:), Levelset(:), Coord(:),TopField(:)
  TYPE(Variable_t), POINTER :: Var, OldVar
  TYPE(Element_t), POINTER :: Element
  TYPE(Nodes_t) :: Nodes
  TYPE(ValueList_t),POINTER :: BC

  
  SAVE Visited,Nodes,Initialized,UnitVector,Coord,MaskExist,MaskPerm,TopPointer,BotPointer,&
      UpPointer,DownPointer,FieldOut,FieldIn,TopNodes,TopPerm, TopField, BotNodes, BotPerm
 
  CALL Info( 'StructuredProjectToPlane','------------------------------------------',Level=4 )
  CALL Info( 'StructuredProjectToPlane','Performing projection on a structured mesh ',Level=4 )
  CALL Info( 'StructuredProjectToPlane','------------------------------------------',Level=4 )

!------------------------------------------------------------------------------
!   Initialize the pointers to top and bottom nodes 
!------------------------------------------------------------------------------

  Debug = .FALSE.
  Params => Solver % Values
  Mesh => Solver % Mesh
  PSolver => Solver


  IF( .NOT. Initialized ) THEN

    IF(Debug) CALL Info('StructuredProjectToPlane','start init')
    at0 = CPUTime()

    ! Choose active direction coordinate and set corresponding unit vector
    !---------------------------------------------------------------------
    PSolver => Solver
    CALL DetectExtrudedStructure( Mesh, PSolver, Var, &
        TopNodePointer = TopPointer, BotNodePointer = BotPointer, &
        UpNodePointer = UpPointer, DownNodePointer = DownPointer )
    MaskExist = ASSOCIATED( Var % Perm ) 
    IF( MaskExist ) MaskPerm => Var % Perm
    Coord => Var % Values
    nsize = SIZE( Coord )
    Initialized = .TRUE.

    TopNodes = 0
    ALLOCATE( TopPerm( Mesh % NumberOfNodes ) )
    TopPerm = 0
    DO i=1,Mesh % NumberOfNodes
      IF(TopPointer(i) == i) THEN
        TopNodes = TopNodes + 1
        TopPerm(i) = TopNodes
      END IF
    END DO
    IF( TopNodes > 0 ) THEN
      ALLOCATE( TopField( TopNodes ) ) 
      TopField = 0.0_dp
    END IF

    BotNodes = 0
    ALLOCATE( BotPerm( Mesh % NumberOfNodes ) )
    BotPerm = 0
    DO i=1,Mesh % NumberOfNodes
      IF(BotPointer(i) == i) THEN
        BotNodes = BotNodes + 1
        BotPerm(i) = BotNodes
      END IF
    END DO

  END IF
  at0 = CPUTime()

  !------------------------------------------------------------------------------
  ! Go through the variables and compute the desired projections
  !------------------------------------------------------------------------------
  GotVar  = .TRUE.
  GotOper = .FALSE.
  NULLIFY(OldVar)
  NoVar = 0
  

  debug = .FALSE.

  DO WHILE(GotVar .OR. GotOper)

    NoVar = NoVar + 1    
    IF(Debug) PRINT *,'NoVar',NoVar

    WRITE (Name,'(A,I0)') 'Variable ',NoVar
    VarName = ListGetString( Solver % Values, TRIM(Name), GotVar )
    NULLIFY(Var)    
    IF(GotVar) THEN
      Var => VariableGet( Model % Variables, TRIM(VarName) )
      IF ( .NOT. ASSOCIATED( Var ) )  THEN
        CALL Fatal('StructuredProjectToPlane','The desired variable does not exist!')
      END IF
    END IF

    IF( ASSOCIATED( Var ) ) THEN
      OldVar => Var
      OldVarName = VarName 
    ELSE IF( ASSOCIATED( OldVar ) ) THEN
      Var => OldVar
      VarName = OldVarName
    ELSE
      EXIT
    END IF

    FieldIn => Var % Values
    PermIn => Var % Perm

    ! Create the projected variable if needed
    !-----------------------------------------------
    WRITE (Name,'(A,I0)') 'Target Variable ',NoVar
    TargetName = ListGetString( Params, TRIM(Name), GotIt )
    IF( .NOT. GotIt ) THEN
      WRITE (TargetName,'(A,A)') TRIM(Oper)//' '//TRIM(VarName)
    END IF

    Var => VariableGet( Mesh % Variables, TRIM(TargetName) )
    IF ( .NOT. ASSOCIATED( Var ) )  THEN      
      NULLIFY(FieldOut)
      WRITE (Name,'(A,I0,A)') 'Target Variable ',NoVar,' At Bottom'
      IF( ListGetLogical( Params, TRIM(Name), GotIt ) ) THEN
        ALLOCATE( FieldOut(BotNodes) )
        CALL VariableAdd( Mesh % Variables, Solver % Mesh, PSolver, &
            TargetName, 1, FieldOut, BotPerm)           
        Var => VariableGet( Mesh % Variables, TRIM(TargetName) )        
      ELSE
        ALLOCATE( FieldOut(TopNodes) )
        CALL VariableAdd( Mesh % Variables, Solver % Mesh, PSolver, &
            TargetName, 1, FieldOut, TopPerm)           
      END IF
      Var => VariableGet( Mesh % Variables, TRIM(TargetName) )
      PRINT *,'Created Target Variable:',TRIM(Var % Name)
    END IF
    FieldOut => Var % Values
    PermOut => Var % Perm    
    FieldOut = 0.0_dp

    TemporalLayer = .TRUE.



   
    WRITE (Name,'(A,I0)') 'Operator ',NoVar
    Oper = ListGetString(Solver % Values,TRIM(Name),GotOper)
    IF(.NOT. GotOper) CYCLE 

    IF(Oper == 'isosurface') THEN
      WRITE (Name,'(A,I0)') 'Isosurface Variable ',NoVar
      LevelsetName = ListGetString(Solver % Values,TRIM(Name),GotIt )
      IF(GotIt) THEN
        Var => VariableGet( Model % Variables, TRIM(LevelsetName) )
        Levelset => Var % Values
        LevelsetPerm => Var % Perm
      ELSE       
        Levelset => Coord
        NULLIFY(LevelsetPerm)
      END IF
      
      WRITE (Name,'(A,I0)') 'Isosurface Value ',NoVar
      Level = ListGetConstReal(Solver % Values,TRIM(Name),GotIt)
    ELSE IF ( Oper(1:5) == 'layer' ) THEN
      WRITE (Name,'(A,I0)') 'Layer Index ',NoVar
      layer = GetInteger( Solver % Values, Name, GotIt )
      IF (.NOT.GotIt) &
        CALL FATAL('StructuredProjectToPlane','no > Layer Index < indicated for output')
    END IF

    ! Operators for dimensional reduction
    !-----------------------------------------------
    SELECT CASE(Oper)      
      
    CASE ('sum')      
      TopField = 0.0_dp
      DO i=1,nsize
        itop = TopPointer(i)
        j = i
        IF(ASSOCIATED(PermIn)) j = PermIn(i)
        IF(j == 0) CYCLE
        val = FieldIn(j)
        TopField(TopPerm(itop)) = TopField(TopPerm(itop)) + val 
      END DO
      
    CASE ('min')      
      TopField = HUGE(TopField)
      DO i=1,nsize
        itop = TopPointer(i)
        j = i
        IF(ASSOCIATED(PermIn)) j = PermIn(i)
        IF(j == 0) CYCLE
        TopField(TopPerm(itop)) = MIN( FieldIn(j),TopField(TopPerm(itop)))
      END DO
      
    CASE ('max')      
      TopField = -HUGE(TopField)
      DO i=1,nsize
        itop = TopPointer(i)
        j = i
        IF(ASSOCIATED(PermIn)) j = PermIn(i)
        IF(j == 0) CYCLE
        TopField(TopPerm(itop)) = MAX( FieldIn(j),TopField(TopPerm(itop)))
      END DO

    CASE ('bottom')
      TopField = 0.0_dp
      DO i=1,nsize
        IF( i == BotPointer(i) ) THEN
          j = i
          IF(ASSOCIATED(PermIn)) j = PermIn(i)
          TopField(TopPerm(TopPointer(i))) = FieldIn(j)
        END IF
      END DO

    CASE ('top')
      TopField = 0.0_dp
      DO i=1,nsize
        IF( i == TopPointer(i) ) THEN
          j = i
          IF(ASSOCIATED(PermIn)) j = PermIn(i)
          TopField(TopPerm(TopPointer(i))) = FieldIn(j)
        END IF
      END DO

    CASE ('layer below top')
      TopField = 0.0_dp
      DO i=1,nsize
        IF( i == TopPointer(i) ) THEN
           l = i
           DO k=1,layer
              l = DownPointer(l)
           END DO
           IF(ASSOCIATED(PermIn)) l = PermIn(l)
           TopField(TopPerm(i)) = FieldIn(l)
        END IF
      END DO

    CASE ('layer above bottom')
      TopField = 0.0_dp
      DO i=1,nsize
        IF( i == BotPointer(i) ) THEN
           l = i
           DO k=1,layer
              l = UpPointer(l)
           END DO
           IF(ASSOCIATED(PermIn)) l = PermIn(l)
           TopField(TopPerm(i)) = FieldIn(l)
        END IF
      END DO

    CASE ('isosurface')
      TopField = 0.0_dp
      DO i=1,nsize
        iup = UpPointer(i)
        j = i
        jup = iup
        IF(ASSOCIATED(LevelsetPerm)) THEN
          j = LevelsetPerm(j)
          jup = LevelsetPerm(jup)
        END IF
        IF(j == 0 .OR. jup == 0) CYCLE

        IF( (Levelset(jup) - Level) * (Levelset(j) - Level) <= 0.0_dp ) THEN
          itop = TopPointer(i)           
          dx = ABS(Levelset(jup) - Levelset(j))           
          IF(ASSOCIATED(PermIn)) THEN
            TopField(TopPerm(itop)) = &
                ABS(Levelset(jup)-Level) * FieldIn(PermIn(i)) / dx + &
                ABS(Level-Levelset(j)) * FieldIn(PermIn(iup)) / dx 
          ELSE
            TopField(TopPerm(itop)) = &
                ABS(Levelset(jup)-Level) * FieldIn(i) / dx + &
                ABS(Level-Levelset(j)) * FieldIn(iup) / dx            
          END IF
        END IF
      END DO
      
    CASE ('int')
      TopField = 0.0_dp
      DO i=1,nsize
        itop = TopPointer(i)
        ! Note for top and bottom this will automatically reduce the distance to half
        !----------------------------------------------------------------------------
        dx = 0.5*(Coord(UpPointer(i)) - Coord(DownPointer(i)))
        j = i
        IF(ASSOCIATED(PermIn)) j = PermIn(i) 
        TopField(TopPerm(itop)) = TopField(TopPerm(itop)) + dx * FieldIn(j)
      END DO

    CASE ('dist')
      TopField = 0.0_dp
      DO i=1,nsize
        itop = TopPointer(i)
        dx = 0.5*(Coord(UpPointer(i)) - Coord(DownPointer(i)))
        TopField(TopPerm(itop)) = TopField(TopPerm(itop)) + dx 
      END DO

    CASE ('index')
      FieldOut = 0.0_dp
      DO i=1,nsize
        IF( i == TopPointer(i) ) THEN
          l = i
          DO k=1,nsize
            IF( ASSOCIATED(PermOut)) THEN
              FieldOut(PermOut(l)) = 1.0_dp * k
            ELSE
              FieldOut(l) = 1.0_dp * k
            END IF
            IF( l == BotPointer(l)) EXIT
            l = DownPointer(l)            
          END DO
        END IF
      END DO
      TemporalLayer = .FALSE.

    CASE default
      CALL Fatal('StructuredProjectToPlane','Unknown operator: '//TRIM(Oper))

    END SELECT


    ! Finally copy the projected values to the target variable
    ! It could be at the top, but it could also be at the bottom, or everywhere
    !----------------------------------------------------------------------------
    IF( TemporalLayer ) THEN
      k = 0
      DO i=1,nsize
        j = i
        IF( ASSOCIATED( PermOut ) ) THEN
          j = PermOut(i)
          IF( j == 0 ) CYCLE
        END IF
        k = TopPerm( TopPointer(i) )
        FieldOut(j) = TopField(k)
      END DO    
    END IF

  END DO

  at1 = CPUTime()

  WRITE(Message,* ) 'Projection time: ',at1-at0
  CALL Info('StructuredProjectToPlane',Message)

!------------------------------------------------------------------------------
END SUBROUTINE StructuredProjectToPlane
!------------------------------------------------------------------------------
